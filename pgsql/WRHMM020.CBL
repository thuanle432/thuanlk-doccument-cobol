000010 IDENTIFICATION                   DIVISION.
000020   PROGRAM-ID.                    WRHMM020.
000030*****************************************************************
000040*
000050*  業務名        ：WebRings  国民健康保険
000060*
000070*  顧客名        ：共通
000080*
000090*  プロセス名    ：個人番号お知らせ通知
000100*
000110*  処理名        ：特定記録バーコード設定
000120*
000130*  処理概要      ：特定記録キー情報ファイルを読み込み、振分区分A、
000140*                  振分区分B、記号番号がキーブレイクするまで
000150*                  スタックし、W個人番号通知の特定記録バーコード
000160*                  番号、作表連番、明細人数をUPDATEする。
000170*
000180*  実行時コード系：SJIS
000190*
000200*  文字コード    ：ﾃﾞｰﾀ半角英数項目：SJIS
000210*
000220*  作成者        ：INES / 金石東
000230*
000240*  案件ID        ：77215
000250*
000260*  作成日        ：2024.05.21
000270*
000280*  修正履歴      ：
000290*    修正年月日  -----------------  修正内容  -------------------
000300*
000310*****************************************************************
000320*****************************************************************
000330 ENVIRONMENT                      DIVISION.
000340*****************************************************************
000350*================================================================
000360 CONFIGURATION                    SECTION.
000370*================================================================
000380 SOURCE-COMPUTER.                 F-PC
000390*DBG*           WITH  DEBUGGING  MODE
000400     .
000410 OBJECT-COMPUTER.                 F-PC.
000420*
000430*================================================================
000440 INPUT-OUTPUT                     SECTION.
000450*================================================================
000460 FILE-CONTROL.
000470*****************************************************************
000480 DATA                             DIVISION.
000490*****************************************************************
000500*================================================================
000510 FILE                             SECTION.
000520*================================================================
000530*================================================================
000540 WORKING-STORAGE                  SECTION.
000550*================================================================
000560*----------------------------------------------------------------
000570*    入出力定義領域
000580*----------------------------------------------------------------
000590*----------------------------------------------------------------
000600*  KAMA002(入力ﾌｧｲﾙのｱｸｾｽ)インターフェース領域
000610*----------------------------------------------------------------
000620*（ＩＮ）
000630* 特定記録キー情報ファイル
000640*
000650   01  PARM-KAMA002.
000660     03  IN-DD-NAME               PIC  X(008)      VALUE 'U01'.
000670     03  STK-LENGTH               PIC S9(009) COMP VALUE +102.
000680     03  TAISHO-HANTEI-MODULE     PIC  X(008)      VALUE SPACE.
000690     03  MAX-STACK-KENSU          PIC S9(009) COMP VALUE +300.
000700     03  STACK-KENSU              PIC S9(009) COMP.
000710     03  DOITSU-KEY-KENSU         PIC S9(009) COMP.
000720     03  INPUT-KENSU              PIC S9(009) COMP.
000730     03  TAISHO-KENSU             PIC S9(009) COMP.
000740     03  KEY-HANTEI               PIC  X(004).
000750     03  KEY-JOHO.
000760         05  KEY-KOSU             PIC S9(009) COMP VALUE +3.
000770         05  DAI-1-KEY.
000780             07  NAGASA           PIC S9(009) COMP VALUE +1.
000790             07  KAISHI-ICHI      PIC S9(009) COMP VALUE +1.
000800         05  DAI-2-KEY.
000810             07  NAGASA           PIC S9(009) COMP VALUE +1.
000820             07  KAISHI-ICHI      PIC S9(009) COMP VALUE +2.
000830         05  DAI-3-KEY.
000840             07  NAGASA           PIC S9(009) COMP VALUE +10.
000850             07  KAISHI-ICHI      PIC S9(009) COMP VALUE +40.
000860*
000870   01  U01-REC.
000880     02  U01REC-I                 OCCURS   300
000890                                  INDEXED  BY  IDX-I.
000900       COPY  WRHMC02.
000910       03  改行コード             PIC X(02).
000920*
000930*================================================================
000940* KAMA021Aインターフェース領域
000950*================================================================
000960*（ＩＮ）
000970* 特定記録バーコードファイル
000980*
000990   01  PARM-KAMA021-U42.
001000     03  KAMA021-U42-DD           PIC  X(008) VALUE  SPACE.
001010     03  KAMA021-U42-PROC         PIC  X(008) VALUE  SPACE.
001020     03  KAMA021-U42-RL           PIC S9(009) COMP-5 VALUE 80.
001030     03  KAMA021-U42-BL           PIC S9(009) COMP-5.
001040     03  KAMA021-U42-KENSU        PIC S9(009) COMP-5.
001050     03  KAMA021-U42-EOF          PIC  X(004) VALUE  SPACE.
001060*
001070   01  U42-REC.
001080     COPY  WRHMC42.
001090*
001100*----------------------------------------------------------------
001110*    ワーク領域
001120*----------------------------------------------------------------
001130   01  ワーク領域.
001140     03  IDX                      PIC  9(003).
001150     03  Ｗ商                     PIC  9(010).
001160     03  Ｗ余り                   PIC  9(001).
001170     03  Ｗ作表連番               PIC  9(006).
001180     03  Ｗ特定記録連番           PIC  9(010).
001190     03  Ｗ特定記録検索           PIC  X(001)  VALUE SPACE.
001200       88  Ｗ特定記録該当あり                  VALUE 'Y'.
001210       88  Ｗ特定記録該当なし                  VALUE 'N'.
001220     03  Ｗ特定記録バーコード値.
001230       05  Ｗ連番                 PIC  9(010).
001240       05  Ｗチェックデジット     PIC  9(001).
001250*
001260     03  WK-SQL-UPDATE-KOJINBANGO.
001270       05  FILLER                 PIC  X(030)  VALUE
001280           "UPDATE   W個人番号通知".
001290       05  FILLER                 PIC  X(040)  VALUE
001300           "  SET  特定記録バーコード番号 = ?".
001310       05  FILLER                 PIC  X(020)  VALUE
001320           " , 作表連番 = ?".
001330       05  FILLER                 PIC  X(020)  VALUE
001340           " , 明細人数 = ?".
001350       05  FILLER                 PIC  X(030)  VALUE
001360           " WHERE  記号番号 = ?  ".
001370*
001380   01  ＮＥＷキー.
001390     03  ＮＥＷ振り分け区分Ａ     PIC  X(001).
001400     03  ＮＥＷ振り分け区分Ｂ     PIC  X(001).
001410   01  ＯＬＤキー.
001420     03  ＯＬＤ振り分け区分Ａ     PIC  X(001).
001430     03  ＯＬＤ振り分け区分Ｂ     PIC  X(001).
001440*
001450   01  カウント領域.
001460     03  入力件数                 PIC  9(009)  VALUE ZERO.
001470     03  特定件数                 PIC  9(009)  VALUE ZERO.
001480     03  更新件数                 PIC  9(009)  VALUE ZERO.
001490     03  入力最初位置             PIC  9(009)  VALUE ZERO.
001500*
001510* 特定記録バーコードテーブル
001520*
001530   01  Ｗ特定記録バーコード.
001540     02  Ｗ特定記録ＴＢＬ         OCCURS   10
001550                                  INDEXED  BY  IDX-T.
001560       COPY  WRHMC42.
001570*
001580   77  Ｗ終了フラグ               PIC  X(003)  VALUE LOW-VALUE.
001590*
001600*----------------------------------------------------------------
001610*    ホスト変数  定義
001620*----------------------------------------------------------------
001630      EXEC SQL BEGIN DECLARE SECTION END-EXEC.
001640*
001650   01  Ｈ特定記録バーコード値     PIC  X(011).
001660   01  Ｈ作表連番                 PIC S9(006).
001670   01  Ｈ同一キースタック件数     PIC S9(006).
001680   01  Ｈ記号番号                 PIC  X(011).
001690*
001700*  ＳＱＬシステム変数
001710     COPY WRHDSQL.
001720*
001730   01  STMVAR                     PIC  X(4096).
001740*
001750*  SERVER パラメータ
001760   01  W-ACCEPT.
001770     03  H-KOKUHO                 PIC  X(003).
001780     03  H-KOKUHO-ADR             PIC  X(015).
001790*
001800      EXEC SQL END   DECLARE SECTION END-EXEC.
001810*
001820*----------------------------------------------------------------
001830*    固定値領域
001840*----------------------------------------------------------------
001850   01  PID                        PIC  X(008) VALUE 'WRHMM020'.
001860*
001870  LINKAGE                         SECTION.
001880   01  PARA-AREA.
001890     03  PARM-LEN                 PIC S9(004) COMP.
001900     03  PARM-KADB-CONN           PIC  X(015).
001910*
001920/****************************************************************
001930 PROCEDURE                        DIVISION
001940                                  USING   PARA-AREA.
001950*****************************************************************
001960*================================================================
001970 ベース処理                       SECTION.
001980*================================================================
001990 ベース処理−開始.
002000*
002010     PERFORM  前処理.
002020*
002030     PERFORM  主処理
002040       UNTIL  ＮＥＷキー  =  HIGH-VALUE.
002050*
002060     PERFORM  後処理.
002070*
002080 ベース処理−終了.
002090     STOP RUN.
002100*================================================================
002110 前処理                           SECTION.
002120*================================================================
002130 前処理−開始.
002140*
002150*------------------------------*
002160* プログラム開始メッセージ表示 *
002170*------------------------------*
002180*
002190     DISPLAY '***  WRHMM020 START  ***'     UPON SYSOUT.
002200*
002210     PERFORM  特定記録スタック.
002220*
002230     PERFORM  ＤＢ接続処理.
002240*
002250     PERFORM  入力読込処理.
002260*
002270 前処理−終了.
002280     EXIT.
002290*================================================================
002300 主処理                           SECTION.
002310*================================================================
002320 主処理−開始.
002330*
002340     MOVE   ZERO                  TO  Ｗ作表連番.
002350     MOVE  ＮＥＷキー             TO  ＯＬＤキー.
002360*
002370     PERFORM  ＤＢ編集処理
002380       UNTIL  ＮＥＷキー    NOT = ＯＬＤキー
002390          OR  ＮＥＷキー      =   HIGH-VALUE.
002400*
002410 主処理−終了.
002420     EXIT.
002430*================================================================
002440 後処理                           SECTION.
002450*================================================================
002460 後処理−開始.
002470*
002480* ＤＢ切断
002490*
002500     EXEC SQL COMMIT END-EXEC.
002510     EXEC SQL DISCONNECT ALL END-EXEC.
002520*
002530*-----------------------------*
002540* プログラム終了メッセージ表示*
002550*-----------------------------*
002560*
002570     MOVE   INPUT-KENSU          TO  入力件数.
002580*
002590     DISPLAY '***(INPUT) U01:特定記録キー情報ファイル　　==> '
002600                                     入力件数    UPON SYSOUT.
002610     DISPLAY '***        U42:特定記録バーコードファイル　==> '
002620                                     特定件数    UPON SYSOUT.
002630     DISPLAY '***(UPDATE) DB:対象記号番号数　　　　　　　==> '
002640                                     更新件数    UPON SYSOUT.
002650     DISPLAY ' '                                 UPON SYSOUT.
002660     DISPLAY '***  WRHMM020 END  ***'            UPON SYSOUT.
002670*
002680 後処理−終了.
002690     EXIT.
002700*================================================================
002710 特定記録スタック                 SECTION.
002720*================================================================
002730 特定記録スタック−開始.
002740*
002750     MOVE  'U42'                  TO  KAMA021-U42-DD.
002760     MOVE  'OPEN'                 TO  KAMA021-U42-PROC.
002770     CALL  'KAMA021A'          USING  PARM-KAMA021-U42.
002780*
002790     INITIALIZE                   Ｗ特定記録バーコード.
002800     MOVE  LOW-VALUE              TO  Ｗ終了フラグ.
002810*
002820     PERFORM  UNTIL  Ｗ終了フラグ  =  HIGH-VALUE
002830        MOVE  'READ'              TO  KAMA021-U42-PROC
002840        CALL  'KAMA021A'       USING  PARM-KAMA021-U42
002850                                      U42-REC
002860*                                          'ｵﾜﾘ'
002870        IF   KAMA021-U42-EOF(1:3)  =  X'B5DCD8'
002880             MOVE  HIGH-VALUE     TO  Ｗ終了フラグ
002890        ELSE
002900             IF   KAMA021-U42-KENSU  >  10
002910                  CONTINUE
002920             ELSE
002930                  DISPLAY  " "                   UPON  SYSOUT
002940                  DISPLAY  KAMA021-U42-KENSU
002950                  " 振り分け区分Ａ = " 振り分け区分Ａ OF U42-REC
002960                  ",特定記録連番 = " 特定記録連番 OF U42-REC
002970                                                 UPON  SYSOUT
002980                  MOVE   U42-REC
002990                    TO   Ｗ特定記録ＴＢＬ(KAMA021-U42-KENSU)
003000             END-IF
003010        END-IF
003020     END-PERFORM.
003030*
003040     IF   KAMA021-U42-KENSU  >  10
003050          DISPLAY  " "                    UPON  SYSOUT
003060          DISPLAY  "＊特定記録バーコードファイル STACK-OVER "
003070                   KAMA021-U42-KENSU "件" UPON  SYSOUT
003080          PERFORM  ＡＢＥＮＤ処理
003090     ELSE
003100          MOVE  KAMA021-U42-KENSU         TO  特定件数
003110     END-IF.
003120*
003130     MOVE    'CLOSE'              TO      KAMA021-U42-PROC.
003140     CALL    'KAMA021A'           USING   PARM-KAMA021-U42.
003150*
003160 特定記録スタック−終了.
003170     EXIT.
003180*================================================================
003190 ＤＢ接続処理                     SECTION.
003200*================================================================
003210 ＤＢ接続処理−開始.
003220*
003230     MOVE   PARM-KADB-CONN        TO  H-KOKUHO-ADR.
003240*
003250     MOVE  'DB1'                  TO  H-KOKUHO.
003260     EXEC   SQL
003270         CONNECT TO :H-KOKUHO-ADR AS :H-KOKUHO
003280     END-EXEC.
003290*
003300     EVALUATE  SQLSTATE
003310       WHEN "00000"
003320       WHEN "01000"
003330           DISPLAY
003340           "国保DB接続に成功（" SQLSTATE " " H-KOKUHO-ADR " )"
003350       WHEN OTHER
003360           DISPLAY
003370           "国保DB接続に失敗（" SQLSTATE " " H-KOKUHO-ADR " )"
003380           CALL  'ABEND'
003390     END-EVALUATE.
003400*
003410 ＤＢ接続処理−終了.
003420     EXIT.
003430*================================================================
003440 入力読込処理                     SECTION.
003450*================================================================
003460 入力読込処理−開始.
003470*
003480     CALL  'KAMA002'       USING  PARM-KAMA002
003490                                  U01-REC.
003500*                                'ｵﾜﾘ'
003510     IF     KEY-HANTEI     =  X'B5DCD8'
003520            MOVE  HIGH-VALUE      TO  ＮＥＷキー
003530            GO  TO                入力読込処理−終了
003540     END-IF.
003550*
003560     COMPUTE  入力最初位置  =  STACK-KENSU  +  1.
003570     MOVE  振り分け区分Ａ OF U01REC-I(1)  TO ＮＥＷ振り分け区分Ａ.
003580     MOVE  振り分け区分Ｂ OF U01REC-I(1)  TO ＮＥＷ振り分け区分Ｂ.
003590*
003600 入力読込処理−終了.
003610     EXIT.
003620*================================================================
003630 ＤＢ編集処理                     SECTION.
003640*================================================================
003650 ＤＢ編集処理−開始.
003660*
003670     ADD    1                     TO  Ｗ作表連番.
003680     PERFORM  特定記録検索処理.
003690*
003700     MOVE  Ｗ作表連番             TO  Ｈ作表連番.
003710     MOVE  Ｗ特定記録バーコード値 TO  Ｈ特定記録バーコード値.
003720     MOVE  STACK-KENSU            TO  Ｈ同一キースタック件数.
003730     MOVE  記号番号 OF U01REC-I(1)
003740                                  TO  Ｈ記号番号.
003750*
003760     PERFORM  ＤＢ更新処理.
003770*
003780     PERFORM  入力読込処理.
003790*
003800 ＤＢ編集処理−終了.
003810     EXIT.
003820*================================================================
003830 特定記録検索処理                 SECTION.
003840*================================================================
003850 特定記録検索処理−開始.
003860*
003870     SET   IDX-T                  TO  1.
003880     MOVE  ZERO                   TO  IDX.
003890*
003900     SEARCH Ｗ特定記録ＴＢＬ
003910       AT END
003920           MOVE  'N'              TO  Ｗ特定記録検索
003930       WHEN ＮＥＷ振り分け区分Ａ  =
003940            振り分け区分Ａ OF Ｗ特定記録ＴＢＬ(IDX-T)
003950           MOVE  'Y'              TO  Ｗ特定記録検索
003960           SET   IDX              TO   IDX-T
003970           MOVE  特定記録連番 OF Ｗ特定記録ＴＢＬ(IDX-T)
003980                                  TO  Ｗ特定記録連番
003990     END-SEARCH.
004000*
004010     IF   Ｗ特定記録該当あり
004020          MOVE  Ｗ特定記録連番    TO  Ｗ連番
004030          DIVIDE  Ｗ連番   BY   7
004040                       GIVING   Ｗ商
004050                    REMAINDER   Ｗ余り
004060          MOVE  Ｗ余り            TO  Ｗチェックデジット
004070          ADD    1                TO  Ｗ特定記録連番
004080          MOVE  Ｗ特定記録連番
004090            TO  特定記録連番      OF  Ｗ特定記録ＴＢＬ(IDX)
004100     ELSE
004110          DISPLAY  " "                   UPON  SYSOUT
004120          DISPLAY  "＊振り分け区分Ａのデータなし "
004130                   "振り分け区分Ａ = " 
004140                   ＮＥＷ振り分け区分Ａ  UPON  SYSOUT
004150          CALL  'ABEND'
004160     END-IF.
004170*
004180 特定記録検索処理−終了.
004190     EXIT.
004200*================================================================
004210 ＤＢ更新処理                     SECTION.
004220*================================================================
004230 ＤＢ更新処理−開始.
004240*
004250     MOVE  SPACE                      TO  STMVAR.
004260     MOVE  WK-SQL-UPDATE-KOJINBANGO   TO  STMVAR.
004270     EXEC  SQL PREPARE STMIDT   FROM :STMVAR  END-EXEC.
004280*
004290     EXEC  SQL EXECUTE STMIDT  USING :Ｈ特定記録バーコード値,
004300                                     :Ｈ作表連番,
004310                                     :Ｈ同一キースタック件数,
004320                                     :Ｈ記号番号
004330     END-EXEC.
004340*
004350     EVALUATE  SQLSTATE
004360         WHEN  "00000"
004370             ADD   1              TO  更新件数
004380         WHEN  OTHER
004390             DISPLAY  " "                   UPON  SYSOUT
004400             DISPLAY  "(" PID ")"
004410                      "W個人番号通知テーブル更新アクセス"
004420                      "エラー（例外）発生"  UPON  SYSOUT
004430             DISPLAY   入力最初位置 "件目レコード "
004440                      "記号番号 = " Ｈ記号番号
004450                                            UPON  SYSOUT
004460             PERFORM  ＳＱＬエラー処理
004470     END-EVALUATE.
004480*
004490 ＤＢ更新処理−終了.
004500     EXIT.
004510*================================================================
004520 ＳＱＬエラー処理                 SECTION.
004530*================================================================
004540 ＳＱＬエラー処理−開始.
004550*
004560     DISPLAY 'SQLSTATE   = ' SQLSTATE  UPON  SYSOUT.
004570     DISPLAY 'SQLMSG     = ' SQLMSG    UPON  SYSOUT.
004580*
004590     EXEC SQL ROLLBACK END-EXEC.
004600     EXEC SQL DISCONNECT ALL END-EXEC.
004610*
004620* 異常終了 プログラムステータスセット
004630     CALL  'ABEND'.
004640*
004650 ＳＱＬエラー処理−終了.
004660     EXIT.
004670*================================================================
004680 ＡＢＥＮＤ処理                   SECTION.
004690*================================================================
004700 ＡＢＥＮＤ処理−開始.
004710*
004720* 各種ファイルクローズ
004730*
004740     MOVE  'CLOSE'                TO   KAMA021-U42-PROC.
004750     CALL  'KAMA021A'          USING   PARM-KAMA021-U42.
004760*
004770* 異常終了 プログラムステータスセット
004780     CALL  'ABEND'.
004790*
004800 ＡＢＥＮＤ処理−終了.
004810     EXIT.
004820****************************************************************
004830*              << WRHMM020 PROGRAM END >>                      *
004840****************************************************************
004850 END PROGRAM WRHMM020.
